#import "utils/options.jsligo" "Options"
#import "errors.jsligo" "Errors"
#import "player.jsligo" "Player"
#import "action.jsligo" "Action"
#import "round_value.jsligo" "RoundValue"
#import "round.jsligo" "Round"
#import "storage.jsligo" "Storage"

const play = (storage:Storage.t, player_address:address, action: Action.t) : Storage.t => {
    const round = Options.get_exn(Storage.get_current_round(storage), Errors.invalid_round);
    const player = Options.get_exn(Storage.get_player(storage, player_address), Errors.user_not_allowed_to_play);

    if (RoundValue.is_waiting(Round.get_round_value(round, player))) {
        return Storage.update_round(storage, Round.play(round, player, action));
    } else {
        return failwith(Errors.user_not_allowed_to_play);
    };
};

const conclude = (storage:Storage.t): Storage.t => {
    const round = Options.get_exn(Storage.get_current_round(storage), Errors.invalid_round);

    if (RoundValue.is_played(Round.get_round_value(round, Player.player1)) && 
        RoundValue.is_played(Round.get_round_value(round, Player.player2))) {
        return Storage.new_game(storage);
    } else {
        return failwith(Errors.cannot_conclude);
    }
};

export type parameter = | ["Play", Action.t] | ["Conclude"];

export const main = (parameter:parameter, storage:Storage.t) : [list<operation>, Storage.t] => {
    match(parameter, {
        Play : p => [list([]) as list<operation>, play(storage, Tezos.sender, p)],
        Conclude: () => [list([]) as list<operation>, conclude(storage)]
    });
};
